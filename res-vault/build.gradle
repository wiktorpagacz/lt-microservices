plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
	id 'io.spring.dependency-management' version '1.1.4'
	id 'com.google.cloud.tools.jib' version '3.4.2'
}

apply plugin: "maven-publish"

task apiJar(type: Jar) {
	from sourceSets.main.output
	include 'com/pagacz/resvault/model/**'
	exclude 'com/pagacz/resvault/model/mappers/**'
}

publishing {
	publications {
		api(MavenPublication) {
			groupId 'com.pagacz'
			artifactId 'res-vault'
			version '0.0.3'

			artifact apiJar
		}
	}
}

def usr =  System.getProperty("duser")
def pass  =  System.getProperty("dpassword")

group = 'com.pagacz'
version = '0.0.1'

java {
	sourceCompatibility = '21'
}

jar {
	duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
	manifest {
		attributes(
				'Main-Class': 'com.pagacz.resvault.ResVaultApplication'
		)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

jib {
	from {
		image = 'openjdk:21'
	}
	to {
		image  = 'registry.hub.docker.com/tanoer/resvaultservice'
		tags = [version]
		auth {
			username = usr
			password = pass
		}
	}
}

repositories {
	mavenLocal()
	mavenCentral()
}

ext {
	set('springCloudVersion', "2023.0.1")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'com.okta.spring:okta-spring-boot-starter:3.0.6'

	implementation 'io.zipkin.reporter2:zipkin-reporter-brave'
	implementation "io.micrometer:micrometer-tracing-bridge-brave"

	implementation 'org.mapstruct:mapstruct:1.5.5.Final'
	implementation 'org.mapstruct:mapstruct-processor:1.5.5.Final'
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor "org.mapstruct:mapstruct-processor:1.5.5.Final"
	compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	useJUnitPlatform()
}
